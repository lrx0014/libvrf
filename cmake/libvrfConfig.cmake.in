# Copyright (c) Microsoft Corporation. All rights reserved.
# Licensed under the MIT license.

# Created variables:
# - LIBVRF_FOUND: YES if libvrf is found and target libvrf::libvrf is available.
# - LIBVRF_INCLUDES_DIR : Directory where the libvrf include files are.
# - LIBVRF_CONFIG_DIR : Directory where the libvrf config files are.
# - LIBVRF_AVAILABLE_TARGETS : Contains a list of available libvrf targets.

@PACKAGE_INIT@

include(CMakeFindDependencyMacro)

# Set our variables.
set(LIBVRF_FOUND NO)
set_and_check(LIBVRF_INCLUDES_DIR "@PACKAGE_libvrf_INCLUDES_INSTALL_DIR@")
set_and_check(LIBVRF_CONFIG_DIR "@PACKAGE_libvrf_CONFIG_INSTALL_DIR@")

# Add the current directory to the module search path to ensure we have
# access to all of our installed helper modules.
list(APPEND CMAKE_MODULE_PATH "${CMAKE_CURRENT_LIST_DIR}")

# Initialize LIBVRF_AVAILABLE_TARGETS
set(LIBVRF_AVAILABLE_TARGETS "")

# Create the targets.
include(${CMAKE_CURRENT_LIST_DIR}/libvrfTargets.cmake)

# Check which targets were created.
if(TARGET libvrf::libvrf)
    # Find our required dependencies.
    find_dependency(OpenSSL 3.2 REQUIRED)

    set(LIBVRF_FOUND YES)
    list(APPEND LIBVRF_AVAILABLE_TARGETS "libvrf::libvrf")
endif()

# Print information about available targets and directories.
if(LIBVRF_FOUND)
    message(STATUS "Found libvrf version ${libvrf_VERSION}.")
endif()
